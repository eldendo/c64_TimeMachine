10 PRINT"+--------------------------+"
20 PRINT"!     SIMPLE MONITOR       !"
30 PRINT"!(C)2017 IR. MARC DENDOOVEN!"
40 PRINT"+--------------------------+"
100 INPUT A
110 FOR I=A TO A+63
115 IF I=8*INT(I/8) THEN H4=I:PRINT:GOSUB 1200:PRINT":";
120 H2=PEEK(I):GOSUB 1100:PRINT" ";
130 NEXT I
132 PRINT
135 A=I
140 GOTO 100
1000 REM *** PRINT HEX DIGIT FROM H1
1010 IF H1<10 THEN PRINT CHR$(H1+ASC("0"));
1020 IF H1>9 THEN PRINT CHR$(H1+ASC("A")-10);
1030 RETURN
1100 REM *** PRINT HEX PAIR FROM H2
1110 H1=INT(H2/16):GOSUB 1000
1120 H1=H2-H1*16:GOSUB 1000
1130 RETURN
1200 REM *** PRINT HEX QUAD FROM H4
1210 H2=INT(H4/256):GOSUB 1100
1220 H2=H4-H2*256:GOSUB 1100
1230 RETURN
