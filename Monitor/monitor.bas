10 PRINT" +-----------------------------------+"
20 PRINT" !        SIMPLE MONITOR V0.1        !"
30 PRINT" !    (C)2017 IR. MARC DENDOOVEN     !"
40 PRINT" +-----------------------------------+"
100 INPUT A
110 FOR R=0 TO 56 STEP 8
120 H4=A+R:PRINT:GOSUB 1200:PRINT": ";
130 FOR K=0 TO 7
140 H2=PEEK(A+K+R):GOSUB 1100:PRINT" ";
150 NEXT K
160 FOR K=0 TO 7
170 P=PEEK(A+K+R)
175 PR=( P>=32 AND P<128 ) OR ( P>159 AND P<192 )
180 IF PR THEN PRINTCHR$(P);
181 IF NOT PR THEN PRINT".";
190 NEXT K
200 NEXT R
210 PRINT
220 A=A+64
230 GOTO 100
1000 REM *** PRINT HEX DIGIT FROM H1
1010 IF H1<10 THEN PRINT CHR$(H1+ASC("0"));
1020 IF H1>9 THEN PRINT CHR$(H1+ASC("A")-10);
1030 RETURN
1100 REM *** PRINT HEX PAIR FROM H2
1110 H1=INT(H2/16):GOSUB 1000
1120 H1=H2-H1*16:GOSUB 1000
1130 RETURN
1200 REM *** PRINT HEX QUAD FROM H4
1210 H2=INT(H4/256):GOSUB 1100
1220 H2=H4-H2*256:GOSUB 1100
1230 RETURN
